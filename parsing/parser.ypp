%{

#include <iostream>
#include <string>
#include <cstdlib>

#include "global.hpp"
#include "struct_builder.hpp"
#include "../structures.hpp" // TODO: Remove

extern int yylex();

extern int yyparse();
extern FILE* cnfin;

bool verbose_parser{false};


%}

%token SOURCE TARGET MAPPING LPAR RPAR COMMA ARROW DOT

%union
{
	int number;
	char *string;
}

%token <number> CONSTANT
%token <string> VARIABLE NAME

%start Start
%%

Start:
	SOURCE Schema TARGET Schema MAPPING Tgds
	{
		const auto &ref = struct_builder::buildFileContent();
		if (verbose_parser) {
			std::cerr << std::endl << "======= FILE PARSED ========" << std::endl << std::endl << ref << std::endl;
			std::cerr << "====== END OF PARSING ======" << std::endl;
		}
	}
;
Schema:
	Schema Relation
	{
		struct_builder::addRelation();
	}
|
	Relation
	{
		struct_builder::changeSchema();
		struct_builder::addRelation();
	}
;
Relation:
	NAME LPAR Atts RPAR
	{
		const auto &ref = struct_builder::constructRelation($1);
		if (verbose_parser) std::cerr << "Read relation :    " << ref << std::endl;
	}
;
Atts:
	Atts COMMA NAME
	{
		struct_builder::addAttribute($3);
	}
|
	NAME
	{
		struct_builder::addAttribute($1);
	}
;
Tgds:
	Tgds Tgd
	{
		struct_builder::addTgd();
	}
|
	Tgd
	{
		struct_builder::addTgd();
	}
;
Tgd:
	Query ARROW Query DOT
	{
		struct_builder::constructTgd();
	}
;
Query:
	Query COMMA Atom
	{
		struct_builder::addAtom();
	}
|
	Atom
	{
		// Change of query
		struct_builder::changeQuery();
		struct_builder::addAtom();
	}
;
Atom:
	NAME LPAR Args RPAR
	{
		const auto &ref = struct_builder::constructAtom($1);
		if (verbose_parser) std::cerr << "Read atom     :    " << ref << std::endl;
	}
;
Args:
	Args COMMA Value
	{
		struct_builder::addValue();
	}
|
	Value
	{
		struct_builder::addValue();
	}
;
Value:
	VARIABLE
	{
		struct_builder::constructValue($1);
	}
|
	CONSTANT
	{
		struct_builder::constructValue($1);
	}
;

%%

int yyerror(char *s) {
	// Du code
	std::cerr << "Error: " << std::string(s);
}